grammar org.uqbarproject.pseudo.Pseudo with org.uqbarproject.pseudo.Tokens

/*
 * 
lambdas? 
mixins
constructores alla scala
sin paquetes
orientado a mensajes
composicion de computaciones x LUEGO y
 * 
 */
generate pseudo "http://www.uqbarproject.org/pseudo/Pseudo"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	types+=Type*;

Type:
	CLASS_TOKEN name=ID (INHERITS_TOKEN superType=[Type])?
	members+=Member*
	END_TOKEN;

Member:
	Attribute | Method;

Method:
    overrideMethod?=OVERRIDE_TOKEN?	(METHOD_TOKEN | classMethod?=CLASS_METHOD_TOKEN) 
		name=ID 
		=> (OPEN_TOKEN ((parameters+=Parameter) (SEPARATOR_TOKEN parameters+=Parameter)*)?  CLOSE_TOKEN)?
	statements+=Statement*
	END_TOKEN;
	
Parameter:
	name = ID
;	

Statement:
	ExpressionLevel4 | Return | LocalVariable ;

Return:
	RETURN_TOKEN value=ExpressionLevel4;
	
LocalVariable:
	LET_TOKEN (name=ID) ('=' (value=ExpressionLevel4))?;

Attribute:
	(ATTRIBUTE_TOKEN|classAttribute?=CLASS_ATTRIBUTE_TOKEN) name=ID ('=' initialValue=ExpressionLevel4)?;

//Max prioirty
ExpressionLevel4 returns Expression:
	ExpressionLevel3 | 
	AssignExpression | 
	IncrementExpression | 
	DecrementExpression 
;

ExpressionLevel3 returns Expression:
	ComparisonExpression 
;

ExpressionLevel2 returns Expression:
	MessageSendExpression |
	ThrowExpression |
	NewExpression |
	InitExpression |
	SetupExpression |
	TraversableExpression | //TODO revise syntax and priority
	WhenExpression |
	TryCatchExpression |
	CollectionLiteralExpression
;

ExpressionLevel1 returns Expression:
	ExpressionLevel0 |
	SuperSend 
;

//Min prioirty
ExpressionLevel0 returns Expression:
	IdExpression |
	StringExpression |
	NumberExpression |
	{NullExpression} NULL_TOKEN |
	{TrueExpression} TRUE_TOKEN |
	{FalseExpression} FALSE_TOKEN |
	{SelfExpression} SELF_TOKEN //|
//	OPEN_TOKEN ExpressionLevel2 => CLOSE_TOKEN
	;
	
IdExpression:
	value=[ecore::EObject]
;	
StringExpression:
	value=STRING
;
	
NumberExpression:
	value=Number
;	

Number:
	INT ('.' INT)?;

MessageSendExpression returns Expression:
	ExpressionLevel1  ({MessageSendExpression.receptor=current} MESSAGE_SEND_TOKEN message=Applicable)* 
;

ComparisonExpression returns Expression:
	ExpressionLevel2 ({ComparisonExpression.left=current} op=ComparisonOperation right=ExpressionLevel2)? 
;

enum ComparisonOperation:
	GT='>' |
	LT='<' |
	GTE= '>=' |
	LTE= '<=' |
	EQ='=' |
	NEQ='/='
;


TraversableExpression:
//TODO ojo que se necesita aplicacion parcial o lambdas
	MaxExpression | //
	MinExpression | //
	AverageExpression | //
	SumExpression | //
	ComprehensionExpression | //
	ForEachExpression
	//	FindExpression |
	//	SortExpression 
;

CollectionLiteralExpression:
	ListLiteralExpression | SetLiteralExpression //| RangeLiteralExpression 
;

ListLiteralExpression:
	{ListLiteralExpression} LIST_OPEN_TOKEN (elements+=ExpressionLevel4 (SEPARATOR_TOKEN elements+=ExpressionLevel4)*)?  LIST_CLOSE_TOKEN	
;

SetLiteralExpression:
	{SetLiteralExpression} SET_OPEN_TOKEN (elements+=ExpressionLevel4 (SEPARATOR_TOKEN elements+=ExpressionLevel4)*)?  SET_CLOSE_TOKEN	
;

//RangeLiteralExpression:
//	'rango desde' start =  EmbeddableExpression 'hasta' end = EmbeddableExpression 
//;


WhenExpression:
	(WHEN_TOKEN (cases+=ExpressionLevel4) 
	THEN_TOKEN actions+= ExpressionLevel4)+ 
	(OTHERWISE_TOKEN defaultAction=ExpressionLevel4)?
	END_TOKEN 
;

TryCatchExpression:
	TRY_TOKEN 
		action= ExpressionLevel4
	CATCH_TOKEN  
		catchAction=ExpressionLevel4	
	END_TOKEN 
;



NewExpression:
	NEW_TOKEN target = [Type] (OPEN_TOKEN (arguments+=ExpressionLevel4 (SEPARATOR_TOKEN arguments+=ExpressionLevel4)*)? => CLOSE_TOKEN)?
;

InitExpression:
	INIT_TOKEN (NEW_TOKEN)? target=[Type]
	(initializations+=Initialization)+  
	END_TOKEN 	
;

SetupExpression:
	SETUP_TOKEN target=ExpressionLevel4
	(initializations+=Initialization)+  
	END_TOKEN 	
;

Initialization:
	INIT_WITH_TOKEN attribute=ID  '='  initialValue=ExpressionLevel4
;

Applicable returns Applicable:
	Message ({Applicable.left=current} (andThen?=AND_THEN_TOKEN | compose?=COMPOSE_TOKEN | or?=OR_TOKEN | and?=AND_TOKEN) right=Message)*
;


Message returns Applicable:
	{Message} selector=ID (OPEN_TOKEN (arguments+=ExpressionLevel4 (SEPARATOR_TOKEN arguments+=ExpressionLevel4)*)? => CLOSE_TOKEN)?
;

SuperSend:
	{SuperSend} SUPER_TOKEN (OPEN_TOKEN (arguments+=ExpressionLevel4 (SEPARATOR_TOKEN arguments+=ExpressionLevel4)*)? => CLOSE_TOKEN)?
;

ThrowExpression:
	(ERROR_TOKEN | THROW_TOKEN) throwable=ExpressionLevel2
;



ComprehensionExpression:
	'generar' (mapping=Applicable 'de')? target=ExpressionLevel1 (=>'tal' 'que' condition=Applicable)?;

ForEachExpression:
	'para' 'cada' ID 'en' (target=ExpressionLevel1) (=>'tal' 'que' condition=Applicable)? action=Applicable;

SumExpression:
	'sumatoria' criteria=Applicable 'de' target=ExpressionLevel1;

AverageExpression:
	'promedio' criteria=Applicable 'de' target=ExpressionLevel1;

MaxExpression:
	'maximo entre' target=ExpressionLevel1 (=>'segun' criteria=Applicable)?;

MinExpression:
	'minimo entre' target=ExpressionLevel1 (=>'segun' criteria=Applicable)?;

AssignExpression:
	ASSIGN_TOKEN (target=[ecore::EObject]) '=' value=ExpressionLevel3;
	
IncrementExpression:
	INCREMENT_TOKEN target=[ecore::EObject] (ACCUM_BY_TOKEN value=ExpressionLevel3)?;
	
DecrementExpression:
	DECREMENT_TOKEN target=[ecore::EObject] (ACCUM_BY_TOKEN value=ExpressionLevel3)?;
	
