grammar org.uqbarproject.pseudo.Pseudo with org.uqbarproject.pseudo.Tokens

/*
 * 
lambdas? 
mixins
constructores alla scala
sin paquetes
orientado a mensajes
composicion de computaciones x LUEGO y
 * 
 */
generate pseudo "http://www.uqbarproject.org/pseudo/Pseudo"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	types+=Type*;

Type:
	CLASS_TOKEN name=ID (INHERITS_TOKEN superType=[Type])?
	members+=Member*
	END_TOKEN;

Member:
	Attribute | Method;

Method:
    overrideMethod?=OVERRIDE_TOKEN?	(METHOD_TOKEN | classMethod?=CLASS_METHOD_TOKEN) 
		name=ID 
		(OPEN_TOKEN ((parameters+=Parameter) (SEPARATOR_TOKEN parameters+=Parameter)*)?  =>CLOSE_TOKEN)?
	statements+=Statement*
	END_TOKEN;
	
Parameter:
	name = ID
;	

Statement:
	Expression | Return | LocalVariable ;

Return:
	RETURN_TOKEN value=EmbeddableExpression;
	
LocalVariable:
	LET_TOKEN (name=ID) ('=' (value=Expression))?;

Attribute:
	(ATTRIBUTE_TOKEN|classAttribute?=CLASS_ATTRIBUTE_TOKEN) name=ID ('=' initialValue=Expression)?;

Expression:
	EmbeddableExpression |
	NonEmbeddableExpression;
	
NonEmbeddableExpression:
	MessageSend |
	SuperSend |
	NewExpression |
	InitExpression |
	SetupExpression |
	TraversableExpression |
	WhenExpression |
	AssignExpression | 
	IncrementExpression | 
	DecrementExpression |
	CollectionLiteralExpression ; //TODO es bastante limitante que no se puedan embeber
	
EmbeddableExpression:
	{IdExpression} value=[ecore::EObject] |
	{StringExpression} value=STRING |
	{NumberExpression} value=Number |
	{NullExpression} NULL_TOKEN |
	{TrueExpression} TRUE_TOKEN |
	{FalseExpression} FALSE_TOKEN |
	{SelfExpression} SELF_TOKEN 
//TODO	soportar? '(' NonEmbeddableExpression ')'
	;

Number:
	INT ('.' INT)?;

TraversableExpression:
//TODO ojo que se necesita aplicacion parcial o lambdas
	MaxExpression | //
	MinExpression | //
	AverageExpression | //
	SumExpression | //
	ComprehensionExpression | //
	ForEachExpression
	//	FindExpression |
	//	SortExpression 
;

CollectionLiteralExpression:
	ListLiteralExpression | SetLiteralExpression //| RangeLiteralExpression 
;

ListLiteralExpression:
	{ListLiteralExpression} LIST_OPEN_TOKEN (elements+=EmbeddableExpression (SEPARATOR_TOKEN elements+=EmbeddableExpression)*)? => LIST_CLOSE_TOKEN	
;

SetLiteralExpression:
	{SetLiteralExpression} SET_OPEN_TOKEN (elements+=EmbeddableExpression (SEPARATOR_TOKEN elements+=EmbeddableExpression)*)? => SET_CLOSE_TOKEN	
;

RangeLiteralExpression:
	'rango desde' start =  EmbeddableExpression 'hasta' end = EmbeddableExpression 
;


WhenExpression:
	(WHEN_TOKEN (cases+=Expression) 
	THEN_TOKEN actions+= Expression)+ 
	(OTHERWISE_TOKEN defaultAction=Expression)?
	END_TOKEN 
;


NewExpression:
	NEW_TOKEN target = [Type] (OPEN_TOKEN (arguments+=EmbeddableExpression (SEPARATOR_TOKEN arguments+=EmbeddableExpression)*)? => CLOSE_TOKEN)?
;

InitExpression:
	INIT_TOKEN (NEW_TOKEN)? target=[Type]
	(initializations+=Initialization)+  
	END_TOKEN 	
;

SetupExpression:
	SETUP_TOKEN target=EmbeddableExpression
	(initializations+=Initialization)+  
	END_TOKEN 	
;

Initialization:
	INIT_WITH_TOKEN attribute=ID  '='  initialValue=Expression
;

MessageSend:
	receptor = EmbeddableExpression MESSAGE_SEND_TOKEN message = Applicable 
;

/*Applicatins are limited to not mix operations on purpose */
Applicable:
	Message |
	ApplicablePipeline |
	ApplicableComposition |
	ApplicableDisjuntion |
	ApplicableConjuntion 
;

ApplicablePipeline:
	 messages+=Message (AND_THEN_TOKEN messages+=Message)+
;

ApplicableComposition:
	messages+=Message (COMPOSE_TOKEN messages+=Message)+
;

ApplicableConjuntion:
	messages+=Message (AND_TOKEN messages+=Message)+
;


ApplicableDisjuntion:
	messages+=Message (OR_TOKEN messages+=Message)+
;

Message:
	selector=ID (OPEN_TOKEN (arguments+=EmbeddableExpression (SEPARATOR_TOKEN arguments+=EmbeddableExpression)*)? => CLOSE_TOKEN)?
;

SuperSend:
	{SuperSend} SUPER_TOKEN (OPEN_TOKEN (arguments+=EmbeddableExpression (SEPARATOR_TOKEN arguments+=EmbeddableExpression)*)? => CLOSE_TOKEN)?
;



ComprehensionExpression:
	'generar' (mapping=Applicable 'de')? target=EmbeddableExpression (=>'tal que' condition=Applicable)?;

ForEachExpression:
	'para cada' ID 'en' (target=EmbeddableExpression) (=>'tal que' condition=Applicable)? action=Applicable;

SumExpression:
	'sumatoria' criteria=Applicable 'de' target=EmbeddableExpression;

AverageExpression:
	'promedio' criteria=Applicable 'de' target=EmbeddableExpression;

MaxExpression:
	'maximo entre' target=EmbeddableExpression (=>'segun' criteria=Applicable)?;

MinExpression:
	'minimo entre' target=EmbeddableExpression (=>'segun' criteria=Applicable)?;

AssignExpression:
	ASSIGN_TOKEN (target=[ecore::EObject]) '=' value=Expression;
	
IncrementExpression:
	INCREMENT_TOKEN target=[ecore::EObject] (ACCUM_BY_TOKEN value=Expression)?;
	
DecrementExpression:
	DECREMENT_TOKEN target=[ecore::EObject] (ACCUM_BY_TOKEN value=Expression)?;
	
