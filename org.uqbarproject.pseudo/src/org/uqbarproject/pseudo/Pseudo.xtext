grammar org.uqbarproject.pseudo.Pseudo with org.eclipse.xtext.common.Terminals

/*
 * 
 * no tipado
en espaÃ±ol
preferir texto
filtrar, mapear, todos, ninguno, para cada a nivel lenguaje
listas literales: Conjunto( ), Lista(), Diccionario( clave : valor )
lambdas? 
sin sobrecarga
mixins
constructores alla scala
sin paquetes
orientado a mensajes
composicion de computaciones x LUEGO y
* variables automaticas?
 * 
 */
generate pseudo "http://www.uqbarproject.org/pseudo/Pseudo"

Model:
	types+=Type*;

Type:
	'clase' name=ID ('hereda de' superType=[Type])?
	declarations+=Declaration*
	'fin' ('clase'?);

Declaration:
	Attribute | Method;

Method:
	'metodo' (classMethod?='de clase'?) name=ID ('(' parameters+=ID* ')')?
	statements+=Expression*
	'fin' ('metodo'?);

Attribute:
	'atributo' (classAttribute?='de clase'?) name=ID (':=' initialValue=Expression)?;

Expression:
	EmbeddableExpression |
	NonEmbeddableExpression;
	
NonEmbeddableExpression:
	SimpleMessageSend |
	TraversableExpression |
	AssignmentExpression |
	IfExpression;
	
EmbeddableExpression:
	SelfUnaryMessageShortcutExpression |
	{IdExpression} value=ID |
	{StringExpression} value=STRING |
	{NullExpression} NULL |
	{NumberExpression} value=Number 
//TODO	soportar? '(' NonEmbeddableExpression ')'
	;

Number:
	INT ('.' INT)?;

TraversableExpression:
//TODO ojo que se necesita aplicacion parcial o lambdas
	MaxExpression | //
	MinExpression | //
	AverageExpression | //
	SumExpression | //
	ComprehensionExpression | //
	ForEachExpression
	//	FindExpression |
	//	SortExpression 
;

IfExpression:
	'si' (condition=Expression) 
	'entonces' trueExpression = Expression 
	'de lo contrario' falseExpression = Expression
	'fin'
;

SimpleMessageSend:
	receptor = EmbeddableExpression ',' message = Message 
;

SelfUnaryMessageShortcutExpression:
	('su'|'sus') message = UnaryMessage
;

Message:
	UnaryMessage //| KeywordMessage
;

UnaryMessage:
	selector=ID //=>(key+=ID argument+=ID)*
;

ComprehensionExpression:
	'generar' (mapping=Message 'de')? target=EmbeddableExpression (=>'tal que' condition=Message)?;

ForEachExpression:
	'para cada' ID 'en' (target=EmbeddableExpression) (=>'tal que' condition=Message)? action=Message;

SumExpression:
	'sumatoria' mapping=Message 'de' target=EmbeddableExpression;

AverageExpression:
	'promedio' mapping=Message 'de' target=EmbeddableExpression;

MaxExpression:
	'maximo entre' target=EmbeddableExpression (=>'segun' condition=Message)?;

MinExpression:
	'minimo entre' target=EmbeddableExpression (=>'segun' condition=Message)?;

AssignmentExpression:
	(assigned=ID) ':=' (value=Expression);

terminal NULL:
	'nada';

terminal RETURN:
	'devolver';


terminal THROW:
	'lanzar';


